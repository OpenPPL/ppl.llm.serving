cmake_minimum_required(VERSION 3.10)

# cmake config without grpc

if(TARGET "ppl_llm_static")
    return()
endif()

add_library(ppl_llm_static STATIC IMPORTED)

# --------------------------------------------------------------------------- #

get_filename_component(__PPL_LLM_PACKAGE_ROOTDIR__ "${CMAKE_CURRENT_LIST_DIR}/../../.." ABSOLUTE)

# --------------------------------------------------------------------------- #

# exported definitions

option(PPL_LLM_ENABLE_LLAMA "" @PPL_LLM_ENABLE_LLAMA@)

if(NOT TARGET "pplnn_basic_static")
    include(${CMAKE_CURRENT_LIST_DIR}/pplnn-config.cmake)
endif()

set(PPL_LLM_LIBRARIES "ppl_llm_static")
set(PPL_LLM_INCLUDE_DIRS ${__PPL_LLM_PACKAGE_ROOTDIR__}/include)
set(PPL_LLM_LINK_DIRS ${__PPL_LLM_PACKAGE_ROOTDIR__}/lib)

# --------------------------------------------------------------------------- #

# sentencepiece

add_library(ppl_sentencepiece_pb_static STATIC IMPORTED)
get_filename_component(__LIB_PATH__ "${__PPL_LLM_PACKAGE_ROOTDIR__}/lib/@HPCC_STATIC_LIB_PREFIX@ppl_sentencepiece_pb_static@HPCC_STATIC_LIB_SUFFIX@" ABSOLUTE)
set_target_properties(ppl_sentencepiece_pb_static PROPERTIES
    INTERFACE_LINK_LIBRARIES "protobuf::libprotobuf"
    IMPORTED_LOCATION "${__LIB_PATH__}"
    IMPORTED_LOCATION_DEBUG "${__LIB_PATH__}"
    IMPORTED_LOCATION_RELEASE "${__LIB_PATH__}")
unset(__LIB_PATH__)

add_library(ppl_sentencepiece_static STATIC IMPORTED)
get_filename_component(__LIB_PATH__ "${__PPL_LLM_PACKAGE_ROOTDIR__}/lib/@HPCC_STATIC_LIB_PREFIX@ppl_sentencepiece_static@HPCC_STATIC_LIB_SUFFIX@" ABSOLUTE)
set_target_properties(ppl_sentencepiece_static PROPERTIES
    INTERFACE_LINK_LIBRARIES "ppl_sentencepiece_pb_static"
    IMPORTED_LOCATION "${__LIB_PATH__}"
    IMPORTED_LOCATION_DEBUG "${__LIB_PATH__}"
    IMPORTED_LOCATION_RELEASE "${__LIB_PATH__}")
unset(__LIB_PATH__)

# --------------------------------------------------------------------------- #

get_filename_component(__LIB_PATH__ "${__PPL_LLM_PACKAGE_ROOTDIR__}/lib/@HPCC_STATIC_LIB_PREFIX@ppl_llm_static@HPCC_STATIC_LIB_SUFFIX@" ABSOLUTE)
set_target_properties(ppl_llm_static PROPERTIES
    INTERFACE_LINK_LIBRARIES "ppl_sentencepiece_static;${PPLNN_LIBRARIES}"
    INTERFACE_LINK_DIRECTORIES "${PPLNN_LINK_DIRS}"
    INTERFACE_INCLUDE_DIRECTORIES "${PPLNN_INCLUDE_DIRS}"
    IMPORTED_LOCATION "${__LIB_PATH__}"
    IMPORTED_LOCATION_DEBUG "${__LIB_PATH__}"
    IMPORTED_LOCATION_RELEASE "${__LIB_PATH__}")
unset(__LIB_PATH__)

# --------------------------------------------------------------------------- #

unset(__PPL_LLM_PACKAGE_ROOTDIR__)
